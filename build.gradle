// Top-level build file where you can add configuration options common to all sub-projects/modules.
import dk.eboks.buildsrc.Libs
import dk.eboks.buildsrc.Versions

buildscript {

    repositories {
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'https://maven.google.com' }
        mavenCentral()
        google()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Nodes.Nstack.translation
        classpath Libs.Nodes.bitrise
        classpath Libs.ribbonizer
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.18.0"
    id 'com.github.ben-manes.versions' version "0.20.0"
}


allprojects {
    repositories {
        maven { url 'https://maven.google.com' }
        maven { url "https://jitpack.io" }
        mavenCentral()
        jcenter()
        google()
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(Versions.ktlint)
        }
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}